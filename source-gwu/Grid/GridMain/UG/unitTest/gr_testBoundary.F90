!!****if* source/Grid/GridMain/UG/unitTest/gr_testBoundary
!!
!! NAME
!!
!!  gr_testBoundary
!!
!! SYNOPSIS
!!
!!  gr_testBoundary(real(:,:,:), intent(INOUT) :: dataBlk,
!!                  integer(MDIM), intent(IN) :: strt,
!!                  integer(MDIM),intent(IN) :: fin,
!!                  integer, intent(IN) :: guard,
!!                  integer, intent(IN) :: axis,
!!                  integer, intent(IN) :: bcType,
!!                  integer, intent(IN) :: face,
!!                  logical, intent(IN) :: isFace)
!!
!!
!! DESCRIPTION
!!
!!  This routine fills up the test block being generated by the unit
!!  test with calculated values of guard cells if they are on 
!!  the physical boundary. These values are then compared against
!!  those filled by Grid functions to verify the correctness of the
!!  Grid funtions.
!!
!! ARGUMENTS
!!
!!  strt  - starting indices 
!!  fin   - ending indices
!!  dataBlk - the unit test data block
!!  guard    - number of guard cell
!!  axis    - the axis under consideration
!!  bcType  - boundary type
!!  face    - whether upper or lower face
!!  isFace  - whether the face corresponds to the face variable
!!            For example if axis is IAXIS and the face variable
!!            is FACEX, then isFace = true 
!!
!! NOTES
!!
!!***

subroutine gr_testBoundary(dataBlk,strt,fin,guard,axis,bcType,face,isFace)

#include "constants.h"
  implicit none

  integer,intent(IN) :: guard,axis,bcType,face
  integer,dimension(MDIM),intent(IN) :: strt,fin
  real,dimension(:,:,:),intent(INOUT) :: dataBlk
  logical, intent(IN) :: isFace

  integer,dimension(guard) :: gr
  integer :: i,j,k,ii,jj,kk,n


  if(bcType==OUTFLOW) then
     if(face==LOW)then
        gr=fin(axis)+1 
     else
        gr=strt(axis)-1
     end if
  elseif(bcType==REFLECTING) then
     if(face==LOW)then
        gr(1)=2*fin(axis)
        if(isFace)gr(1)=gr(1)+1
     else
        gr(1)=strt(axis)-1
        if(isFace)gr(1)=gr(1)-1
     end if
     do i=2,guard
        gr(i)=gr(i-1)-1
     end do
  end if

  n=1
  do k=strt(KAXIS),fin(KAXIS)
     kk=k
     if(axis==KAXIS)then
        kk=gr(n)
        n=n+1
     end if
     if(axis==JAXIS)n=1
     do j=strt(JAXIS),fin(JAXIS)
        jj=j
        if(axis==JAXIS)then
           jj=gr(n)
           n=n+1
        end if
        if(axis==IAXIS)n=1
        do i = strt(IAXIS),fin(IAXIS)
           ii=i
           if(axis==IAXIS)then
              ii=gr(n)
              n=n+1
           end if
           dataBlk(i,j,k)=dataBlk(ii,jj,kk)
        end do
     end do
  end do
  return
end subroutine gr_testBoundary
