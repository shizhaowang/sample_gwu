!!****if* source/Simulation/SimulationMain/TwoGamma/Grid_applyBCEdge
!!
!! NAME
!!  Grid_applyBCEdge
!!
!! SYNOPSIS
!!
!!  Grid_applyBCEdge(integer(IN)              :: bcType, 
!!                   integer(IN)              :: bcDir,
!!                   integer(IN)              :: guard,
!!                   integer(IN)              :: var,
!!                   real(INOUT),dimension(:) :: dataRow(2*guard),
!!                   integer(IN)              :: face,
!!                   integer(IN)              :: gridDataStruct,
!!                   integer(IN),OPTIONAL     :: blockHandle,
!!                   real(in),OPTIONAL    :: secondCoord,
!!                   real(in),OPTIONAL    :: thirdCoord)
!!  
!!                    
!!  
!! DESCRIPTION 
!!  
!!  Applies the boundary conditions to a given vector.
!!  This routine applies the boundary conditions on a given face (lowerface
!!  or upperface) of a given vector. 
!!     If (face=LOW)dataRow(1:guard) = boundary values
!!     If (face=HIGH) dataRow(guard+1:2*guard) = boundary values
!!  The reason why information about direction and variable is included in
!!  this interface is because velocities need to be treated specially
!!  for REFLECTING boundary conditions. 
!!  This implementation is specific to the TwoGamma problem which 
!!  requires inflow and outflow boundary conditions.
!!
!!
!!  
!! ARGUMENTS 
!!
!!
!!  bcType -   the type of boundary condition being applied to this face
!!              -  USER_DEFINED: This routine does its own thing.
!!              -  other types:  This routine reproduces the actions of the Grid unit's
!!                 own implementation (at least for cases that may occur in the
!!                 TwoGamma simulation.)     
!!  bcDir -    can take on values IAXIS,JAXIS or KAXIS. This is needed
!!             for handling the reflective boundary conditions. If bcDir=IAXIS,
!!             and boundary conditions are reflective, and X velocity is
!!             treated differently from all other variables. similarly if bcDir
!!             is JAXIS, then Y velocity is different.
!!  guard -    number of guardcells 
!!  var   -    The variable on which boundary conditions are applied
!!             It is used with bcDir for reflective boundary conditions
!!             to correctly handle velocities. This argument is redundant 
!!             for all other variables.
!!  dataRow -  storage for the data being operated upon.
!!  face    -  can take values LOW and HIGH, defined in constants.h
!!             to indicate whether to apply boundary on lowerface or 
!!             upperface
!!  gridDataStruct : integer value specifying data structure. 
!!                   The options are defined in constants.h and they are :
!!                   CENTER cell centered variables (unk or work for PM) (default)
!!                   FACEX  face centered variable on faces along IAXIS
!!                   FACEY  face centered variable on faces along JAXIS
!!                   FACEZ  face centered variable on faces along IAXIS
!!
!!  NOTES 
!!            This routine exists in the simulation directory of 
!!            the TwoGamma problem, and therefore replaces
!!            the default implementation in the Grid unit.
!!            
!!
!!***


subroutine Grid_applyBCEdge(bcType,bcDir,guard,var,dataRow,face,&
     gridDataStruct, blockHandle, secondCoord,thirdCoord)
  use Simulation_data, ONLY : sim_rho1,sim_rho2,sim_temp1,sim_temp2,&
                              sim_gammac1,sim_gammac2,&
                              sim_p0,sim_cvelx,sim_int1,sim_small
  use Driver_interface, ONLY : Driver_abortFlash
  use Grid_data, ONLY :gr_myPE                   ! only needed for print* in error case
  implicit none

# include "constants.h"
# include "Flash.h"

  integer, intent(in):: bcType
  integer,intent(IN) :: var,guard,face,bcDir,gridDataStruct
  real,dimension(:),intent(INOUT)::dataRow
  integer,intent(IN),OPTIONAL :: blockHandle
  real,intent(IN),OPTIONAL :: secondCoord,thirdCoord
  integer :: i     !loop counter

  if (gridDataStruct/=CENTER) then
     print*,'boundary is',bcType,gr_myPE,face,gridDataStruct
     call Driver_abortFlash("[Grid_applyBCEdge] Simulation TwoGamma does not support face variables")
  end if

  if((bcType)==PERIODIC) return

  if(face==LOW) then
     select case (bcType)
     case(OUTFLOW)
        do i = 1,guard
           dataRow(i)= dataRow(guard+1)
        end do
     case(USER_DEFINED)
        select case(var)
        case(GAMC_VAR)
           dataRow(1:guard)=sim_gammac1
        case(DENS_VAR)
           dataRow(1:guard)=sim_rho1
        case(PRES_VAR)
           dataRow(1:guard)=sim_p0
        case(VELX_VAR)
           dataRow(1:guard)=sim_cvelx
        case(VELY_VAR)
           dataRow(1:guard)=0.0
        case(VELZ_VAR)
           dataRow(1:guard)=0.0
        case(ENER_VAR)
           dataRow(1:guard)=max(0.5*(sim_cvelx**2)+sim_int1,sim_small)
        case(EINT_VAR)
           dataRow(1:guard)=max(sim_int1,sim_small)
        case(SPECIES_BEGIN)
           dataRow(1:guard)=1.0e0-sim_small
        case(SPECIES_BEGIN+1)
           dataRow(1:guard)=sim_small
        end select
     case default
        call Driver_abortFlash("unsupported boundary condition on Lower Face")
     end select
  else
     
     select case (bcType)
        
     case(OUTFLOW)
        do i = 1,guard
           dataRow(guard+i)= dataRow(guard)
        end do
     case(USER_DEFINED)
        print*,'boundary is',bcType,gr_myPE,face
        call Driver_abortFlash("Simulation TwoGamma does not support USER_DEFINED boundary on Upper Face")
     case default
        print*,'boundary is',bcType,gr_myPE
        call Driver_abortFlash("unsupported boundary condition on Upper Face")
     end select
  end if
  return
end subroutine Grid_applyBCEdge

