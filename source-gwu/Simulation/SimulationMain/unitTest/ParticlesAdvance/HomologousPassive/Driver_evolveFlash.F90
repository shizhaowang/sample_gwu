!!****if* source/Simulation/SimulationMain/unitTest/ParticlesAdvance/HomologousPassive/Driver_evolveFlash
!!
!! NAME
!!
!!  Driver_evolveFlash
!!
!! SYNOPSIS
!!
!!  Driver_evolveFlash()
!!
!! DESCRIPTION
!!
!! This routine implements the Strang splitting scheme for time
!! advancement. A single step in the this driver 
!! includes two sweeps, the first one in order XYZ, and
!! the second one in order ZYX. This driver works with directionally
!! split operators only. The routine also controls the regridding of
!! the mesh if necessary and the simulation output.
!!
!!  This is a modification of the standard Driver_evolveFlash for the unitTest for 
!!     the Particles unit.  In this test, instead of using Hydro sweeps in xyz/zyx,
!!     the velocities are generated by an explicitly given field.
!!  
!!
!! NOTES
!!
!!
!!
!!***


#ifdef DEBUG_ALL
#define DEBUG_DRIVER
#endif


subroutine Driver_evolveFlash()

  use Driver_data, ONLY: dr_globalMe, dr_nbegin, &
       dr_nend, dr_dt, dr_wallClockTimeLimit, &
       dr_tmax, dr_simTime, dr_fSweepDir, dr_rSweepDir,&
       dr_nstep, dr_dtOld, dr_dtNew, dr_restart, dr_elapsedWCTime

  use Driver_interface, ONLY : Driver_computeDt, Driver_getElapsedWCTime
  use Logfile_interface, ONLY : Logfile_stamp, Logfile_close
  use Timers_interface, ONLY : Timers_start, Timers_stop, &
    Timers_getSummary
  use Particles_interface, ONLY : Particles_advance, Particles_unitTest
  use Grid_interface, ONLY : Grid_getLocalNumBlks, &
    Grid_getListOfBlocks, Grid_updateRefinement
  use IO_interface, ONLY :IO_output,IO_outputFinal
  use sim_interface
  use Simulation_data, ONLY : sim_analyticParticlePositions, &
       sim_schemeOrder,&
       sim_maxTolCoeff0,sim_maxTolCoeff1,sim_maxTolCoeff2,sim_maxTolCoeff3

  implicit none

#include "constants.h"
#include "Flash.h"

  integer   :: localNumBlocks, i

  integer :: blockCount
  integer :: blockList(MAXBLOCKS)

  
  ! for logfile output
  character(len=MAX_STRING_LENGTH), dimension(4,2) :: strBuff
  character(len=15) :: numToStr


  logical ::  perfect = .true.

  character(len=20) :: fileName
  integer, parameter        :: fileUnit = 2
  integer,dimension(4) :: prNum
  integer :: temp
  real :: maxActualDt = 0.0
  real :: maxError

  logical :: endRun !Should we end our run on this iteration?

  ! stays true if no errors are found
  perfect = .true.
  
  temp = dr_globalMe
  
  do i = 1,4
     prNum(i)= mod(temp,10)
     temp = temp/10
  end do
  filename = "unitTest_"//char(48+prNum(4))//char(48+prNum(3))//&
                                 char(48+prNum(2))//char(48+prNum(1))
  
  open(fileUnit,file=fileName)
  write(fileUnit,'("P",I0)') dr_globalMe

  call Logfile_stamp( 'Entering evolution loop' , '[Driver_evolveFlash]')


  call Timers_start("evolution")

!!******************************************************************************
!! Start of Evolution Loop
!!******************************************************************************

  do dr_nstep = dr_nbegin, dr_nend


     !!Step forward in time. See bottom of loop for time step calculation.
     
     call Grid_getLocalNumBlks(localNumBlocks)
     call Grid_getListOfBlocks(LEAF,blockList,blockCount)
     if (dr_globalMe == MASTER_PE) then

        write (numToStr(1:), '(I10)') dr_nstep
        write (strBuff(1,1), "(A)") "n"
        write (strBuff(1,2), "(A)") trim(adjustl(numToStr))
        
        write (numToStr(1:), "(1PE12.6)") dr_simTime
        write (strBuff(2,1), "(A)") "t"
        write (strBuff(2,2), "(A)") trim(adjustl(numToStr))
        
        write (numToStr(1:), "(1PE12.6)") dr_dt
        write (strBuff(3,1), "(A)") "dt"
        write (strBuff(3,2), "(A)") trim(adjustl(NumToStr))
        
        call Logfile_stamp( strBuff(1:3,:), 3, 2, "step")

     end if

     !!--------------------------------------------------------------------
     !!- Start Physics Sequence
     !!--------------------------------------------------------------------
     maxActualDt = max(maxActualDt, dr_dt)

     dr_simTime = dr_simTime + dr_dt

     call Particles_unitTest(fileUnit,perfect)

!!******************************************************************************
!!Second "half-step" of the evolution loop
!!******************************************************************************

     dr_simTime = dr_simTime + dr_dt

     call Particles_unitTest(fileUnit,perfect)


     !--------------------------------------------------------------------
     !- End Physics Sequence -- Start Simulation Bookkeeping
     !--------------------------------------------------------------------

     !output a plotfile before the grid changes
     call IO_output( dr_simTime, &
          dr_dt, dr_nstep+1, dr_nbegin, endRun, PLOTFILE_AND_PARTICLEFILE)


     !!if (itemp_limit) .eq. 1) call Hydro_timstepPrecompute()


     dr_dtOld = dr_dt                     ! backup needed old 
     ! calculate new

     call Timers_start("compute dt")

     call Driver_computeDt(dr_nbegin, dr_nstep, &
                         dr_simTime, dr_dtOld, dr_dtNew)
     call Timers_stop("compute dt")
     dr_dt = dr_dtNew                                        ! store new
     

     !!-----------------------------------------------------------------
     !! Output for current step in evolution
     !!-----------------------------------------------------------------

     call Timers_start("IO_output")
     call IO_output( dr_simTime, &
          dr_dt, dr_nstep+1, dr_nbegin, endRun, CHECKPOINT_FILE_ONLY)
     call Timers_stop("IO_output")

!!*****************************************************************************
!!  Evolution Loop -- check termination conditions
!!******************************************************************************


     !Exit if a .dump_restart or .kill was found during the last step
     if(endRun) exit


     !! the simulation ends before nend iterations if
     !!  (i)   the simulation time is greater than the maximum time (tmax)
     !!  (ii)  the redshift falls below the minimum redshift  
     !!        (also called redshiftFinal) 
     !!  (iii) the wall clock time is greater than the maximum 
     !!        (wall_clock_time_max)

     
     if (dr_simTime >= dr_tmax) then
        if(dr_globalMe == MASTER_PE) then
           print *, "exiting: reached max SimTime"
        end if
        exit
     end if
     
     call Driver_getElapsedWCTime(dr_elapsedWCTime)
     if (dr_elapsedWCTime >  dr_wallClockTimeLimit) then
        if(dr_globalMe == MASTER_PE) then
           print *, "exiting: reached max wall clock time"
        end if
        exit
     end if

  enddo

!!******************************************************************************
!! End of Evolution Loop
!!******************************************************************************



  call Timers_stop("evolution")

  call Logfile_stamp( 'Exiting evolution loop' , '[Driver_evolveFlash]')

  !if a file termination, this may already be done.
  if(.NOT.endRun) call IO_outputFinal( )

  call Timers_getSummary( dr_nstep)




  !finish unit test write out file

  call sim_getComputedError(maxError)

  print*,dr_globalMe,maxError,sim_maxTolCoeff0,&
       sim_maxTolCoeff1 * maxActualDt   ,&
       sim_maxTolCoeff2 * maxActualDt**2,&
       sim_maxTolCoeff3 * maxActualDt**3
  if (sim_schemeOrder == 0) then
     if (maxError > sim_maxTolCoeff0) perfect = .FALSE.
  end if
  if (sim_schemeOrder == 1) then
     if (maxError > sim_maxTolCoeff1 * maxActualDt) perfect = .FALSE.
  end if
  if (sim_schemeOrder == 2) then
     if (maxError > sim_maxTolCoeff2 * maxActualDt**2) perfect = .FALSE.
  end if
  if (sim_schemeOrder == 3) then
     if (maxError > sim_maxTolCoeff3 * maxActualDt**3) perfect = .FALSE.
  end if

  if (perfect) then
    write(fileUnit,'("Particles unitTest PASSED!  all results conformed with expected values.")')
    write(*,'("Particles unitTest PASSED!  all results conformed with expected values.")')
    call Logfile_stamp( "Particles unitTest PASSED!")
  else
    write(fileUnit,'("Particles unitTest FAILED!")')
    write(*,'("Particles unitTest FAILED!")')
    call Logfile_stamp( "Particles unitTest FAILED!")
  endif

  close(fileUnit)

  call Logfile_stamp( "FLASH run complete.", "LOGFILE_END")

  call Logfile_close()


  return
  
end subroutine Driver_evolveFlash



